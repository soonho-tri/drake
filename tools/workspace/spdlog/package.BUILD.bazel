# -*- python -*-

load(
    "@drake//tools/install:install.bzl",
    "cmake_config",
    "install",
    "install_cmake_config",
)

licenses(["notice"])  # MIT

package(
    default_visibility = ["//visibility:public"],
)

config_setting(
    name = "linux",
    values = {"cpu": "k8"},
    visibility = ["//visibility:private"],
)

cc_library(
    name = "spdlog",
    srcs = glob(["src/**"]),
    hdrs = glob(
        ["include/spdlog/**"],
        exclude = ["include/spdlog/fmt/bundled/**"],
    ),
    defines = [
        "HAVE_SPDLOG",
        # Use the compiled (not header-only) variant of spdlog for build speed.
        "SPDLOG_COMPILED_LIB",
        # Use Drake's @fmt external, not the bundled version inside spdlog.
        "SPDLOG_FMT_EXTERNAL",
    ],
    includes = ["include"],
    linkopts = select({
        ":linux": ["-pthread"],
        # This is a bazel-default rule, and does not need @drake//
        "@//conditions:default": [],
    }),
    # Upstream's default is to ship static (not shared), so we'll do the same.
    linkstatic = True,
    deps = ["@fmt"],
)

CMAKE_PACKAGE = "spdlog"

cmake_config(
    package = CMAKE_PACKAGE,
    script = "@drake//tools/workspace/spdlog:package-create-cps.py",
    version_file = "include/spdlog/version.h",
    deps = ["@fmt//:cps"],
)

# Creates rule :install_cmake_config.
install_cmake_config(package = CMAKE_PACKAGE)

install(
    name = "install",
    targets = [":spdlog"],
    hdr_dest = "include/" + CMAKE_PACKAGE,
    hdr_strip_prefix = ["include"],
    guess_hdrs = "PACKAGE",
    docs = ["LICENSE"],
    deps = [":install_cmake_config"],
)
